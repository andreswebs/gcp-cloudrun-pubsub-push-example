data "google_secret_manager_secret_version" "mongod_atlas_key" {
  secret = var.mongodb_atlas_secret
}

provider "mongodbatlas" {
  private_key = data.google_secret_manager_secret_version.mongod_atlas_key.secret_data
  public_key  = var.mongodb_atlas_pubkey
}


resource "mongodbatlas_project" "this" {
  org_id = var.mongodb_atlas_org_id
  name   = var.mongodb_atlas_project_name
}

resource "random_password" "mongodb_user" {
  length           = 32
  numeric          = true
  min_numeric      = 1
  upper            = true
  min_upper        = 1
  lower            = true
  min_lower        = 1
  special          = true
  min_special      = 0
  override_special = "_-"
}


resource "google_secret_manager_secret" "mongodb_atlas_password" {
  secret_id = var.mongodb_atlas_password_secret
  replication {
    automatic = true
  }
}

resource "google_secret_manager_secret_version" "mongodb_atlas_password" {
  secret      = google_secret_manager_secret.mongodb_atlas_password.id
  secret_data = random_password.mongodb_user.result
}

locals {
  mongodb_database_name = "app"
  mongodb_username      = "app"
}

resource "mongodbatlas_database_user" "mongodb_user" {
  username           = local.mongodb_username
  password           = google_secret_manager_secret_version.mongodb_atlas_password.secret_data
  project_id         = mongodbatlas_project.this.id
  auth_database_name = "admin"
  roles {
    role_name     = "readWrite"
    database_name = local.mongodb_database_name
  }
}